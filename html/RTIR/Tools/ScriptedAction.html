%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
<& /RTIR/Elements/Header, Title => $Title &>
<& /RTIR/Tools/Elements/Tabs, 
    current_tab => $current_tab, 
    current_subtab => $current_subtab, 
    Title => $Title &>

<& /Elements/ListActions, actions => \@finalresults &>

<form method="get" action="ScriptedAction.html" name="ScriptedAction" enctype="multipart/form-data">
<input type="hidden" name="incidentid" value="<%$incidentid%>" />
<input type="hidden" name="loop" value="<%$ARGS{'loop'}%>" />
<input type="hidden" name="UpdateType" value="response" />
<input type="hidden" name="Action" value="Respond" />
<input type="hidden" name="Owner" value="<%$session{'CurrentUser'}->Id%>" />
<input type="hidden" name="id" value="new" />
<input type="hidden" name="Status" value="<%$ARGS{Status}||'open'%>" />
<a name="top"></a>
        
% if ( $ARGS{'TestAddresses'} && keys %$addrhash ) {
<&| /Widgets/TitleBox, contentbg => "#cccccc", 
    title => 'Address test results' &>
<ul>
%   foreach my $key (keys %$addrhash) {
  <li><%$addrhash->{$key}->{'IP'}%>: <%$addrhash->{$key}->{'Address'}%>
%   }
</ul>
</&>
<br />
% }

<&| /Widgets/TitleBox, contentbg => "#cccccc", title => loc("Identify Recipients") &>

<table border="0" cellpadding="0" cellspacing="2">
% if ($loop eq 'IP') {
  <tr>
    <td class="label">WHOIS:</td>
  <td>
  <table>
  <tr>
    <td class="input">
      <select name="server">
<%PERL>
foreach my $entry ( map $whois_servers->{ $_ }, sort keys %{$whois_servers} ) {
    next unless $entry;

    my ($host, $name) = ref($entry)
        ? ($entry->{'Host'}, $entry->{'FriendlyName'})
        : ($entry, $entry);
    next unless $host;

    my $selected = "";
    $selected = " selected" if $host eq $server;
</%PERL>
        <option value="<% $host %>" <% $selected %>><% $name || $host %></option>
% }
      </select>
  </tr>
  </table>
  </td>
  </tr>
  <tr>
    <td class="label">
      <&|/l&>Contact Field</&>:
    </td>
    <td>
      <input name="field" value="<% $ARGS{'field'} %>" />
    </td>
  </tr>
  <tr>
    <td class="labeltop">
      <&|/l&>IP Addresses</&>:<br /><small>arg: <i>_IP_</i></small>
    </td>
    <td colspan="5">
% if (exists $ARGS{IPs}) {
      <& /Elements/MessageBox, Name=>"IPs", Width => 30, Default=>$ARGS{IPs}, IncludeSignature => 0, %ARGS&>
% } else {
      <& /Elements/MessageBox, Name=> 'IPs', Width => 30, IncludeSignature => 0 &>
% }
    </td>
  </tr>
% } else {
  <tr>
    <td class="labeltop">
      <&|/l&>Addresses</&>:<br /><small>arg: <i>_ADDR_</i></small>
    </td>
    <td colspan="5">
% if (exists $ARGS{Addresses}) {
      <& /Elements/MessageBox, Name=>"Addresses", Width => 30, Default=>$ARGS{Addresses}, IncludeSignature => 0, %ARGS&>
% } else {
      <& /Elements/MessageBox, Name=> 'Addresses', Width => 30, IncludeSignature => 0 &>
% }
    </td>
  </tr>
% }
<tr>
  <td class="label">
    <&|/l&>Template</&>:
  </td>
  <td>
    <& /Admin/Elements/SelectTemplate, Name => "Template", Default => $ARGS{'Template'}, Queue => $investigationq->Id &>
  </td>
</tr>
<tr>
  <td class="label">
    <&|/l&>Argument</&>:
  </td>
  <td>
    <input name="TemplateArg" size="60" value="<% $ARGS{'TemplateArg'} %>" />
  </td>
</tr>
  <tr>
    <td align="right" colspan="2">
    </td>
  </tr>
</table>
% if ($loop eq "IP") {
<& /Elements/Submit, Name => "TestAddresses", Caption => loc("Test email addresses"), Label => loc("Test")&>
% }
</&>

<br />

<&| /Widgets/TitleBox, contentbg => "#cccccc", title => loc("Message information") &>

<table border="0" cellpadding="0" cellspacing="2">
  <tr>
    <td class="label">
      <&|/l&>Owner</&>:
    </td>
    <td>
      <%$session{'CurrentUser'}->Name%>
    </td>
  </tr>

% if (!$incidentid) {
<& /RTIR/Incident/Elements/Create, Title => $Title, QueueObj => $incidentq, %ARGS &>
% } else {
  <tr>
    <td class="label">
      <&|/l&>Subject</&>:
    </td>
    <td>
      <input size="60" name="Subject" value="<%$ARGS{'Subject'} || $Incident->Subject%>" />
    </td>
  </tr>
% }

<tr>
  <td class="labeltop">
    <&|/l&>Message</&>:
  </td>
  <td colspan="5">
% if (exists $ARGS{Content}) {
<& /Elements/MessageBox, Name=>"Content", Default=>$ARGS{Content}, %ARGS&>
% } else {
<& /Elements/MessageBox, Name=>"Content", %ARGS &>
% }
  </td>
</tr>
</table>

</&>
<& /Elements/Submit, Name => "SubmitAction", Caption => loc("Create the Incidents and Investigations"), Label => loc("Create")&>
</form>

<%INIT>
my ($Incident, $Ticket);
my @finalresults;

my $Title;
if ( $incidentid ) {
    $Title = loc("Scripted Action: Create Investigations for Incident #[_1]", $incidentid);
} else {
    $Title = loc("Scripted Action: Create Incidents and Investigations");
}

# XXX: what's this?
foreach my $arg (keys %ARGS) {
  if ($arg =~ /^Requestorbox-(.+)/) {
    $ARGS{'Addresses'} .= $1 . "\r";
  }
}

if ($incidentid) {
    $Incident = RT::Ticket->new($session{'CurrentUser'});
    $Incident->Load($incidentid);

    if (!exists $ARGS{Content}) {
        my $Transactions = $Incident->Transactions;

        if ($Transactions) {
            $ARGS{Content} = $m->scomp("/RTIR/Elements/TransactionData", 
               Transactions => $Transactions,
               Type => 'messages',
               Include => {'Create', 'Correspond'},
               QuoteText => 1);
            $ARGS{Content} =~ s/\&gt;/>/g;
        }
    }
}

$ARGS{'loop'} = $loop if !defined $ARGS{'loop'};

if ($loop eq 'address') {
    $current_subtab = "RTIR/Tools/ScriptedAction.html",
} else {
    $current_subtab = 'RTIR/Tools/ScriptedAction.html?loop=IP';
}

# get the list of WHOIS servers
my $whois_servers = RT->Config->Get('whois');
unless ( $server ) {
    ($server) =
        grep defined && length,
        map ref( $_ )? $_->{'Host'} : $_,
        map $whois_servers->{ $_ },
        sort keys %$whois_servers;
}

# Load the Incidents Queue
my $incidentq = RT::Queue->new($session{'CurrentUser'});
unless ( $incidentq->Load('Incidents') ) {
    Abort('Queue not found');
}

# Load the Investigations Queue
my $investigationq = RT::Queue->new($session{'CurrentUser'});        
unless ($investigationq->Load('Investigations')) {
    Abort('Queue not found');
}

# Load the Incident Reports Queue
my $reportq = RT::Queue->new($session{'CurrentUser'});        
unless ($reportq->Load('Incident Reports')) {
    Abort('Queue not found');
}

my $addrhash = {};

if ( $ARGS{'SubmitAction'} || $ARGS{'TestAddresses'} ) {
    my $list = $loop eq 'IP'? $ARGS{'IPs'} : $ARGS{'Addresses'};
    $list =~ s/\r+\n/\n/g;
    $list =~ s/^\s+//;
    $list =~ s/\s+$//;
    # replace newlines with commas (for use with parse_csv)
    $list =~ s/\n/,/g;

    if ($loop eq 'address') {
        my @addrs = parse_csv( $list );
        my $i = 0;
        foreach my $addr (@addrs) {
            $addrhash->{$i} = {Address => $addr, IP => loc("IP_UNKNOWN")};
            $i++;
        }
    } elsif ($loop eq 'IP') {
        my @ips = parse_csv( $list );
        my $i = 0;
        foreach my $ip (@ips) {
            my ($a, $err);
            $m->comp('Elements/GetEmailFromIP',
                server => $ARGS{'server'},
                q => $ip,
                field => $ARGS{'field'},
                address => \$a,
                error => \$err,
            );
            if ($err) {
                # no addresses
                push @finalresults, loc("Couldn't get address for [_1]: [_2]", $ip, $err);
                next;
            }
            $addrhash->{$i} = {Address => $a, IP => $ip};
            $i++;
        }
    }
}

push @finalresults, $m->comp('/RTIR/Create.html:ProcessAttachments', %ARGS);

if ( $ARGS{'SubmitAction'} ) {

    # if the subject contains _ADDR_ or _IP_ we will substitute
    my $original_subject = $ARGS{'Subject'};
    my $original_content = $ARGS{'Content'};

    foreach my $key ( keys %$addrhash ) {
        my $addr = $addrhash->{$key}->{'Address'};
        my $ip = $addrhash->{$key}->{'IP'};

        $ARGS{'Subject'} = $original_subject;
        $ARGS{'Content'} = $original_content;

        for (qw/Subject Content/) {
            $ARGS{$_} =~ s/_ADDR_/$addr/g;
            $ARGS{$_} =~ s/_IP_/$ip/g;
        }

        if ( $addr eq loc("ADDRESS_UNKNOWN") ) {
            # create an unlinked incident report
            unless ( $reportq->CurrentUserHasRight('CreateTicket') ) {
                Abort('You have no permission to create tickets in that queue.');
            }
            my ($Ticket, @ChildActions) = CreateTicket( %ARGS, Queue => $reportq->Id );
            unless ( $Ticket ) {
                Abort("Couldn't create an Incident Report");
            }
            unless ( $Ticket->CurrentUserHasRight('ShowTicket') ) {
                Abort("No permission to view newly created ticket #".$Ticket->id.".");
            }
            push @finalresults, @ChildActions;

            next;
        }

        if ( !$Incident || !$incidentid ) {
            # create the Incident
            unless ( $incidentq->CurrentUserHasRight('CreateTicket') ) {
                Abort('You have no permission to create tickets in that queue.');
            }
            my @IncidentActions;
            ($Incident, @IncidentActions) = CreateTicket(
                %ARGS,
                Queue       => $incidentq->Id,
                Attachments => $session{'Attachments'},
            );
            unless ( $Incident ) {
                Abort("Couldn't create an Incident");
            }
            unless ( $Incident->CurrentUserHasRight('ShowTicket') ) {
                Abort("No permission to view newly created ticket #".$Incident->id.".");
            }
            push @finalresults, @IncidentActions;
        }

        # create the linked investigation
        unless ( $investigationq->CurrentUserHasRight('CreateTicket') ) {
            Abort('You have no permission to create tickets in that queue.');
        }
        my ($Ticket, @ChildActions) = CreateTicket(
            %ARGS,
            Queue          => $investigationq->Id,
            'new-MemberOf' => $Incident->Id,
            Requestor      => $addr,
        );
        unless ( $Ticket ) {
            Abort("Couldn't launch an Investigation");
        }
        unless ( $Ticket->CurrentUserHasRight('ShowTicket') ) {
            Abort("No permission to view newly created ticket #".$Ticket->id.".");
        }
        push @finalresults, @ChildActions;

        my $TemplateObj = RT::Template->new( $session{'CurrentUser'} );
        if ( $ARGS{'Template'} ) {
            $TemplateObj->Load( $ARGS{'Template'} );
            $RT::Logger->error("Couldn't load template '$ARGS{'Template'}'")
                unless $TemplateObj->id;
        }

        my @updateresults;
        if ( $TemplateObj->id ) {

            # find the Create transaction, so we can pass it to the template
            my $transaction;
            while ( $transaction = $Ticket->Transactions->Next ) {
                last if $transaction->Type eq 'Create';
            }

            # if the template's argument contains _ADDR_ or _IP_
            # substitute appropriately
            my $arg = $ARGS{'TemplateArg'};
            $arg =~ s/_ADDR_/$addr/g;
            $arg =~ s/_IP_/$ip/g;

            my ( $result, $message ) = $TemplateObj->Parse(
                Argument       => $arg,
                TicketObj      => $Ticket,
                TransactionObj => $transaction,
            );
            unless ( $result ) {
                $RT::Logger->error("Couldn't parse template: $message");
            }

            # add the template's MIMEObj to the Attachments list
            $session{'Attachments'} = {
                %{$session{'Attachments'} || {}},
                _Body => $TemplateObj->MIMEObj,
            };

            # the content will be part of the template, if desired
            $ARGS{'UpdateContent'} = " ";
            $ARGS{'UpdateAttachments'} = $session{'Attachments'};
            @updateresults = ProcessUpdateMessage(
                TicketObj => $Ticket, ARGSRef => \%ARGS,
            );
        }

        push @finalresults, map { loc("Ticket [_1]: [_2]", $Ticket->Id, $_) }
            @updateresults;

        # delete attachments only after they've been sent to everyone
        delete $session{'Attachments'};
    }
    # restore back arguments
    $ARGS{'Subject'} = $original_subject;
    $ARGS{'Content'} = $original_content;
}

sub parse_csv {
    my $text = shift;      # record containing comma-separated values
    my @new  = ();
    push(@new, $+) while $text =~ m{
        # the first part groups the phrase inside the quotes.
        # see explanation of this pattern in MRE
        "([^\"\\]*(?:\\.[^\"\\]*)*)",?
           |  ([^,]+),?
           | ,
       }gx;
       push(@new, undef) if substr($text, -1,1) eq ',';
       return @new;      # list of values that were comma-separated
}


</%INIT>

<%ARGS>
$incidentid => undef
$Requestors => undef
$server => undef
$current_tab => "RTIR/Tools/ScriptedAction.html"; 
$current_subtab => undef
$loop => 'address'
</%ARGS>
